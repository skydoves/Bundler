plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
}

apply from: '../dependencies.gradle'
apply from: '../publish.gradle'

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        versionCode versions.versionCode
        versionName versions.versionName
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        buildConfig false
    }
}

kotlin {
    explicitApi()
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-XXLanguage:+InlineClasses"]
    kotlinOptions.freeCompilerArgs += ["-Xopt-in=kotlin.OptIn"]
}

tasks.withType(Javadoc) {
    excludes = ['**/*.kt']
    options.addBooleanOption('Xdoclint:none', true)
}

dependencies {
    implementation "androidx.appcompat:appcompat:$versions.androidxAppcompat"

    // unit test
    testImplementation "junit:junit:$versions.junit"
    testImplementation "androidx.test:core:$versions.androidxTest"
    testImplementation "org.robolectric:robolectric:$versions.robolectric"
}

apply from: '../spotless.gradle'